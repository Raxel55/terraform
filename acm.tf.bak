resource "aws_acm_certificate" "kanda" {
  domain_name       = "kandasoft-dev.com"
  validation_method = "DNS"

  tags = local.common_tags

  lifecycle {
    create_before_destroy = true
  }
}


resource "aws_route53_zone" "kanda" {
  name = "kandasoft-dev.com"
}

resource "aws_route53_record" "kanda-common" {
  for_each = {
    for dvo in aws_acm_certificate.kanda.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  allow_overwrite = true
  name            = each.value.name
  records         = [each.value.record]
  ttl             = 60
  type            = each.value.type
  zone_id         = aws_route53_zone.kanda.zone_id
}

resource "aws_route53_record" "kanda-website" {
  zone_id = aws_route53_zone.kanda.zone_id
  name    = "kandasoft-dev.com"
  type    = "A"

  alias {
    name                   = aws_lb.kanda.dns_name
    zone_id                = aws_lb.kanda.zone_id
    evaluate_target_health = true
  }
}
